        ; **************************************
        ; * Video mode configuration functions *
        ; **************************************

        ;---------------------------------------------------------------------
        ; Copies TVC program file from Cart ROM to RAM
        ; Input:  HL - ROM address
        ;         DE - RAM address
        ;         BC - Number of bytes to copy
        ; Output: HL - last byte address of ROM file
        ;         DE - last address of the RAM
        ; Destroys: HL, BC, DE, A, F

SetPaging_U0_U1_VID_SYS:
        ld	a, P_U0_U1_VID_SYS      ; Page in video memory to page 2.: U1, U2, VID, SYS
        jp      SetPaging

SetPaging_U0_U1_U2_SYS:
        ld	a, P_U0_U1_U2_SYS	; Set paging to U0, U1, U2, SYS
        jp      SetPaging

SetPaging_U0_U1_U2_U3:
        ld	a, P_U0_U1_U2_U3        ; Set paging to U0, U1, U2, U3
        jp      SetPaging

SetPaging_U0_U1_VID_U3:
        ld	A,P_U0_U1_VID_U3
        jp      SetPaging

SetPaging:
	ld	(P_SAVE), a
	out	(PAGE_REG), a
        ret

        ;---------------------------------------------------------------------
        ; Changes the value of a BASIC string variable
        ; Input:  HL - New string value (zero terminated)
        ;         DE - Name of the variable
        ;          C - Length of the variable name
LetStringVariable:
        push    hl                      ; push string pointer
        ld      hl,(CHAIN)              ; load symbol area address

SerachForSymbol:
        push    hl                      ; save current symbol address
        inc     hl                      ; skip next symbol address
        inc     hl
        ld      a, (hl)                 ; load symbol name length
        cp      a, c                    ; compare with length of the symbol to search 
        jr      nz, NextSymbol         ; Length are different -> next symbol
        push    de                      ; save symbol name pointer

        ; compare symbol name
        ld      b, a
        inc     hl                      ; skip symbol length

CompareSymbol: 
        ld      a, (de)
        cp      (hl)
        jr      nz, SymbolNameDifferent
        inc     hl
        inc     de
        djnz    CompareSymbol

        ; symbol name found
        ld      a, (hl)                  ; load symbol type
        cp      a, 0
        jr      nz, WrongSymbolType

        ; copy value string
        inc     hl                      ; skip type
        inc     hl                      ; skip allocated length
        ex      hl, de                  ; move destination to de
        pop     hl
        pop     hl
        pop     hl                      ; reload string pointer

        push    de                      ; store length position
        inc     de                      ; first character position
        ld      b, 0                    ; string length

CopyNextCharacter:
        ld      a, (hl)                 ; load character
        or      a
        jr      z, StringEnd
        ld      (de), a
        inc     hl
        inc     de
        inc     b
        jr      CopyNextCharacter

StringEnd:
        pop     hl
        ld      (hl),  b                ; store length

        ret

WrongSymbolType:
SymbolNameDifferent:
        pop     de                      ; reload symbol name pointer
        pop     hl                      ; reload current address pointer
        pop     hl                      ; reload string pointer

        ret

NextSymbol:
        pop     hl                      ; reload current symbol address
        ld      a, (hl)                 ; load next symbol address
        inc     hl
        ld      h, (hl)
        ld      l, a
        ld      a, h
        cp      a, $c0
        jr      c, SerachForSymbol

        ; symbol not found -> do nothing
        pop     hl                      ; reload string pointer
        ret  

        ;---------------------------------------------------------------------
        ; Draws a horizontal line
        ; Input:  B - X 
        ;         C - Y
        ;         A - Length
DrawHorizontalLine:
        call    PosToVidmemAddress

        ld      b, a
        ld      a, (CharBackgroundSave1+1)

DrawHorizontalLineL1:
        ld      (hl), a
        inc     hl
        djnz    DrawHorizontalLineL1

        ret

        ; **************************************
        ; * ZX7 decompression                  *
        ; **************************************

        ; -----------------------------------------------------------------------------
        ; ZX7 decoder by Einar Saukas & Urusergi
        ; "Turbo" version (88 bytes, 25% faster)
        ; -----------------------------------------------------------------------------
        ; Parameters:
        ;   HL: source address (compressed data)
        ;   DE: destination address (decompressing)
        ; -----------------------------------------------------------------------------
dzx7_turbo:		
        ld	a,$80

dzx7t_copy_byte_loop:	
        ldi				; copy literal byte

dzx7t_main_loop:	
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        jr	nc,dzx7t_copy_byte_loop	; next bit indicates either literal or sequence
                        
        ; determine number of bits used for length (Elias gamma coding)
        push	de
        ld	bc,1
        ld	d,b

dzx7t_len_size_loop:	
        inc	d
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        jr	nc,dzx7t_len_size_loop
        jp	dzx7t_len_value_START

        ; determine length
dzx7t_len_value_loop:	
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        rl	c
        rl	b
        jr	c,dzx7t_exit		; check end marker

dzx7t_len_value_START:	
        dec	d
        jr	nz,dzx7t_len_value_loop
        inc	bc			; adjust length
                        
        ; determine offset
        ld	e,(hl)			; load offset flag (1 bit) + offset value (7 bits)
        inc	hl
        sll	e			; opcode for undocumented instruction "SLL E" aka "SLS E"
        jr	nc,dzx7t_offset_end	; if offset flag is set, load 4 extra bits
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        rl	d			; insert first bit into D
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        rl	d			; insert second bit into D
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        rl	d			; insert third bit into D
        add	a,a			; check next bit
        call	z,dzx7t_load_bits	; no more bits left?
        ccf
        jr	c,dzx7t_offset_end
        inc	d			; equivalent to adding 128 to DE

dzx7t_offset_end:	
        rr	e			; insert inverted fourth bit into E
                        
        ; copy previous sequence
        ex	(sp),hl			; store source, restore destination
        push	hl			; store destination
        sbc	hl,de			; HL = destination - offset - 1
        pop	de			; DE = destination
        ldir
dzx7t_exit:		
        pop	hl			; restore source address (compressed data)
        jp	nc,dzx7t_main_loop
                        
dzx7t_load_bits:	
        ld	a,(hl)			; load another group of 8 bits
        inc	hl
        rla
        ret

SAVED_MEM_PAGES  db     0
SAVED_GRAPH_MODE db     0

        ;---------------------------------------------------------------------
        ; Copy file name from zero terminated string to TVC format (length+string)
        ; Input:  HL - Source address of the zero terminated string
        ;         DE - Destination address of the file name
CopyFileName:
        push    de
        inc     de
        ld      b, 0

LCopyFileNameLoop:
        ld      a, (hl)                 ; check terminating zero
        or      a
        jr      z, LCopyFileNameExit

        ld      (de), a                 ; copy file name chacter
        inc     hl
        inc     de

        inc     b                       ; update and check file name length
        ld      a, b
        cp      a, FILE_NAME_LENGTH
        jr      c, LCopyFileNameLoop

LCopyFileNameExit
        pop     hl                      ; store file name length
        ld      (hl), b
        
        ret

        ;---------------------------------------------------------------------
        ; Append extension to the file name
        ; Input:  HL - Source address of the zero terminated extension string
        ;         DE - Address of the file name buffer
AppendFileNameExtension:
        push    de
        ex      hl,de
        ld      c, (hl)                 ; get current file name length
        ld      b, 0
        inc     hl
        add     hl, bc
        ex      hl, de
        
LAppendFileNameExtensionLoop:
        ld      a, (hl)
        or      a
        jr      z, LAppendFileNameExtensionExit

        ld      (de), a
        inc     hl
        inc     de
        inc     c
        ld      a, c
        cp      a, FILE_NAME_LENGTH
        jr      c, LAppendFileNameExtensionLoop

LAppendFileNameExtensionExit:
        pop     hl
        ld      (hl), c
        
        ret

        ;---------------------------------------------------------------------
        ; Loads a binary file from the file system
        ; Input:  HL - Destination address of the file
        ;         DE - Name of the file
        ; Output: A - Error code, 0 = no error
        ;         HL - Length of the file
LoadBinaryFile:
        ld      (SAVED_DESTINATION_ADDRESS), hl         ; save destination address

        rst     30h                                     ; open file
        db      CAS_FN_OPEN

        or      a                                       ; return if error occured
        ret     nz

        rst     30h                                     ; load file length low byte
        db      CAS_FN_CHIN

        or      a
        ret     nz

        ld      a, c
        ld      (SAVED_FILE_LENGTH), a

        rst     30h                                     ; load file length high byte
        db      CAS_FN_CHIN

        or      a
        ret     nz

        ld      a, c
        ld      (SAVED_FILE_LENGTH+1), a

        ld      bc, (SAVED_FILE_LENGTH)                 ; load binary file
        ld      de, (SAVED_DESTINATION_ADDRESS)
        rst     30h
        db      CAS_FN_BKIN

        or      a
        ret     nz

        rst     30h                                     ; close file
        db      CAS_FN_CLOSE_RD

        ld      hl, (SAVED_FILE_LENGTH)

        ret

SAVED_DESTINATION_ADDRESS        dw      0               ; destination address save for load binary file function
SAVED_FILE_LENGTH                dw      0               ; saved binary file length

